plugins {
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'com.gradleup.shadow' version '8.3.5'
}

version = System.getenv('GITHUB_VERSION') ?: 'local'
group = 'org.mvplugins.multiverse.portals'
description = 'Multiverse-Portals'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        name = 'spigotmc'
        url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/')
    }

    maven {
        name = 'onarandombox'
        url = uri('https://repo.onarandombox.com/content/groups/public')
    }

    maven {
        name = 'enginehub'
        url = uri('https://maven.enginehub.org/repo/')
    }

    maven {
        // todo: remove before mv5 release
        name = 'benwoo1110'
        url = uri('https://repo.c0ding.party/multiverse-beta')
    }
}

dependencies {
    // Spigot
    implementation('org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT') {
        exclude group: 'junit', module: 'junit'
    }

    // Core
    implementation 'org.mvplugins.multiverse.core:multiverse-core:5.0.0-SNAPSHOT'

    //WorldEdit
    implementation('com.sk89q.worldedit:worldedit-bukkit:7.2.9') {
        exclude group: 'org.bukkit', module: 'bukkit'
    }

    // Utils
    api('com.dumptruckman.minecraft:Logging:1.1.1') {
        exclude group: 'org.bukkit', module: 'bukkit'
    }
}


java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}


configurations {
    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
        it.outgoing.artifact(shadowJar)
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Multiverse/Multiverse-Portals"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}


processResources {
    def props = [version: "${project.version}"]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }

    // This task should never be skipped. The tests depend on this having been run but we want the new version number
    // that is created after tests are run and before we run again to publish.
    outputs.upToDateWhen { false }
}


checkstyle {
    toolVersion = '6.1.1'
    configFile file('config/mv_checks.xml')
    ignoreFailures = true
}


javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
}


project.configurations.api.canBeResolved = true

shadowJar {
    relocate 'com.dumptruckman.minecraft.util.Logging', 'org.mvplugins.multiverse.portals.util.MVPLogging'
    relocate 'com.dumptruckman.minecraft.util.DebugLog', 'org.mvplugins.multiverse.portals.util.DebugFileLogger'

    configurations = [project.configurations.api]

    archiveClassifier.set('')
}

build.dependsOn shadowJar
jar.enabled = false


tasks.register('runHabitatGenerator', JavaExec) {
    classpath = configurations["compileClasspath"]
    mainClass.set('org.mvplugins.multiverse.external.jvnet.hk2.generator.HabitatGenerator')

    args = [
            '--file', "build/libs/multiverse-portals-$version" + ".jar",
            '--locator', 'Multiverse-Portals',
    ]
}
tasks.named("build") { finalizedBy("runHabitatGenerator") }
