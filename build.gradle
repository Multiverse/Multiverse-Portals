plugins {
    id 'checkstyle'
    id 'org.mvplugins.multiverse-plugin' version '1.2.0'
}

group = 'org.mvplugins.multiverse.portals'
description = 'Multiverse-Portals'

repositories {
    maven {
        name = 'enginehub'
        url = uri('https://maven.enginehub.org/repo/')
    }
}

configure(apiDependencies) {
    serverApiVersion = '1.18.2-R0.1-SNAPSHOT'
    mockBukkitServerApiVersion = '1.21'
    mockBukkitVersion = '4.31.1'
}

dependencies {
    // Multiverse
    externalPlugin 'org.mvplugins.multiverse.core:multiverse-core:5.0.0-SNAPSHOT'

    // WorldEdit
    externalPlugin('com.sk89q.worldedit:worldedit-bukkit:7.2.9') {
        exclude group: 'org.bukkit', module: 'bukkit'
    }

    // Utils
    shadowed('com.dumptruckman.minecraft:Logging:1.1.1') {
        exclude group: 'junit', module: 'junit'
    }

    // hk2 for annotation processing only
    compileOnly('org.glassfish.hk2:hk2-api:3.0.3') {
        exclude group: '*', module: '*'
    }
    annotationProcessor 'org.glassfish.hk2:hk2-metadata-generator:3.0.3'
    testAnnotationProcessor 'org.glassfish.hk2:hk2-metadata-generator:3.0.3'
}

checkstyle {
    toolVersion = '6.1.1'
    configFile file('config/mv_checks.xml')
    ignoreFailures = true
}

shadowJar {
    relocate 'com.dumptruckman.minecraft.util.Logging', 'org.mvplugins.multiverse.portals.util.MVPLogging'
    relocate 'com.dumptruckman.minecraft.util.DebugLog', 'org.mvplugins.multiverse.portals.util.DebugFileLogger'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Multiverse/Multiverse-Portals"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
