plugins {
    id 'checkstyle'
    id 'org.mvplugins.multiverse-plugin' version '1.2.0'
}

group = 'org.mvplugins.multiverse.portals'
description = 'Multiverse-Portals'

repositories {
    maven {
        name = 'enginehub'
        url = uri('https://maven.enginehub.org/repo/')
    }

    maven {
        // todo: remove before mv5 release
        name = 'benwoo1110'
        url = uri('https://repo.c0ding.party/multiverse-beta')
    }
}

configure(apiDependencies) {
    serverApiVersion = '1.18.2-R0.1-SNAPSHOT'
    mockBukkitServerApiVersion = '1.21'
    mockBukkitVersion = '4.31.1'
}

dependencies {
    // Multiverse
    externalPlugin 'org.mvplugins.multiverse.core:multiverse-core:5.0.0-SNAPSHOT'

    // WorldEdit
    externalPlugin('com.sk89q.worldedit:worldedit-bukkit:7.2.9') {
        exclude group: 'org.bukkit', module: 'bukkit'
    }

    // Utils
    shadowed('com.dumptruckman.minecraft:Logging:1.1.1') {
        exclude group: 'junit', module: 'junit'
    }
}

checkstyle {
    toolVersion = '6.1.1'
    configFile file('config/mv_checks.xml')
    ignoreFailures = true
}

shadowJar {
    relocate 'com.dumptruckman.minecraft.util.Logging', 'org.mvplugins.multiverse.portals.util.MVPLogging'
    relocate 'com.dumptruckman.minecraft.util.DebugLog', 'org.mvplugins.multiverse.portals.util.DebugFileLogger'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Multiverse/Multiverse-Portals"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }

        maven {
            // todo: remove before mv5 release
            name = "multiverseBeta"
            url = "https://repo.c0ding.party/multiverse-beta"
            credentials(PasswordCredentials)
        }
    }
}
